plugins {
  id "com.itiviti.dotnet" version "1.7.2"
  id 'com.nwalsh.gradle.saxon.saxon-gradle' version '0.9.3'
}

defaultTasks "dist"

import com.nwalsh.gradle.saxon.SaxonXsltTask

// Find dotnet
def dotnetex = null
// Laboriously construct a search path that includes some default locations
// plus the users actual path.
def spath = ["/usr/local/share/dotnet"]
System.getenv("PATH").split(System.getProperty("path.separator")).each { dir ->
  spath += [dir]
}
spath.each { dir ->
  if (dotnetex == null) {
    def fn = new File(dir + "/dotnet")
    if (fn.exists() && fn.canExecute()) {
      dotnetex = fn.toString()
    } else {
      fn = new File(dir + "/dotnet.exe")
      if (fn.exists() && fn.canExecute()) {
        dotnetex = fn.toString()
      }
    }
  }
}

if (dotnetex == null) {
  println("WARNING: Failed to find dotnet[.exe]!")
}

// You must specify the key and source externally in order to push the
// nuget package. For example, they can be read from
// ~/.gradle/gradle.properties. They must not be committed to the
// repository!
if (!hasProperty("nugetApiKey")) {
  ext.nugetApiKey = "KEYREQUIREDTOPUSH"
}
if (!hasProperty("nugetSource")) {
  ext.nugetSource = "SOURCEREQUIREDTOPUSH"
}

dotnet {
  dotnetExecutable = dotnetex
  solution = 'XmlResolverData/XmlResolverData.sln'
  configuration = 'Release'
  verbosity = 'Normal'
  projectName = 'XmlResolverData'

  restore {
  }

  build {
  }  

  nugetPush {
    apiKey = nugetApiKey
    source = nugetSource
  }

  /* I can't figure out how NUnit tests are supposed to be
     integrated into the build; I have them in a separate
     solution, but that's apparently not standard?
  test {
    collectCoverage = true
    nunit {
      testOutputXml = file("${buildDir}/reports/nunit")
    }
  }
   */
}

task copy_data(type: Copy) {
  into "${projectDir}/XmlResolverData/XmlResolverData/Data"
  from ("${projectDir}/data/src/data") {
    exclude "manifest.xml"
  }
}

task make_data_catalog(type: SaxonXsltTask, dependsOn: ["copy_data"]) {
  input "${projectDir}/data/src/data/manifest.xml"
  output "${projectDir}/XmlResolverData/XmlResolverData/Data/catalog.xml"
  stylesheet "${projectDir}/tools/make-catalog.xsl"
}
dotnetBuild.dependsOn make_data_catalog

task make_data_tests(type: SaxonXsltTask, dependsOn: ["copy_data"]) {
  outputs.file "${projectDir}/XmlResolverData/UnitTests/DataTest.cs"
  input "${projectDir}/data/src/data/manifest.xml"
  output "${projectDir}/XmlResolverData/XmlResolverData/Data/catalog.xml"
  stylesheet "${projectDir}/tools/make-catalog.xsl"
  parameters (
    'generate-tests': "${projectDir}/XmlResolverData/UnitTests/DataTest.cs"
  )
}

task make_data_csproj(type: SaxonXsltTask, dependsOn: ["copy_data"]) {
  input "${projectDir}/data/src/data/manifest.xml"
  output "${projectDir}/XmlResolverData/XmlResolverData/XmlResolverData.csproj"
  stylesheet "${projectDir}/tools/make-csproj.xsl"
}
dotnetBuild.dependsOn make_data_csproj

task dist(type: Exec, dependsOn: ["dotnetBuild"]) {
  environment "CSHARP_XMLRESOLVER_ROOT", projectDir
  commandLine dotnetex, "test", "XmlResolverData/UnitTests/UnitTests.csproj"
}

task helloWorld() {
  doLast {
    println("Hello, world.")
  }
}
